set windows-shell := ["powershell.exe", "-NoLogo", "-Command"]
daemon_contract_dir := "./daemon/internal/contract"
api_contract_dir := "./api/internal/contract"

# Convention:
# 1. Recipe names starting and ending with underscore (_)
#    are meant to be "private" and should not be called directly.

[no-cd]
[unix]
_create_dirs_ path:
  mkdir -p {{path}}

[no-cd]
[windows]
_create_dirs_ path:
  [System.IO.Directory]::CreateDirectory("{{path}}")

_genabi_:
  just _create_dirs_ {{api_contract_dir}}
  just _create_dirs_ {{daemon_contract_dir}}
  abigen --abi ./contract/build/P2M.abi --pkg contract --type P2MContract --out {{api_contract_dir}}/p2m_contract.go
  cp {{api_contract_dir}}/p2m_contract.go {{daemon_contract_dir}}/p2m_contract.go

[unix]
[working-directory:"contract"]
compile:
  rm -rf temp/ build/
  solc --bin --abi --optimize --overwrite P2M.sol -o temp/
  just _create_dirs_ build
  mv temp/P2M.* build/
  rm -rf temp/
  just _genabi_

[windows]
[working-directory:"contract"]
compile:
  'temp','build' | % { if (Test-Path $_) { Remove-Item -Recurse -Force $_ } }
  solc --bin --abi --optimize --overwrite P2M.sol -o temp
  just _create_dirs_ build
  Move-Item temp\P2M.* build
  Remove-Item -Recurse -Force temp
  just _genabi_

genabi: compile _genabi_

[working-directory:"ignite"]
deploy: compile
  go run .
